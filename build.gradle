import org.gradle.internal.os.OperatingSystem

plugins {
	id "java"
	id "com.github.johnrengelman.shadow" version "8.1.1"
}

java {
	withSourcesJar()
}

allprojects {
	apply plugin: "java"

	java {
		sourceCompatibility = "17"
		targetCompatibility = "17"
	}

	repositories {
		mavenCentral()
	}
	
	project.ext.jomlVersion = "1.10.1"
	project.ext.lwjglVersion = "3.3.4"
	project.ext.coroutinesVersion = "1.7.3"
	switch (OperatingSystem.current()) {
		case OperatingSystem.LINUX:
			def osArch = System.getProperty("os.arch")
			project.ext.lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
					? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
					: "natives-linux"
			break
		case OperatingSystem.WINDOWS:
			def osArch = System.getProperty("os.arch")
			project.ext.lwjglNatives = osArch.contains("64")
					? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
					: "natives-windows-x86"
			break
		case OperatingSystem.MAC_OS:
			project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
			break
	}
}

dependencies {
	testImplementation platform("org.junit:junit-bom:5.10.0")
	testImplementation "org.junit.jupiter:junit-jupiter:5.10.0"
	testRuntimeOnly("org.junit.platform:junit-platform-launcher")
	
	compileOnly platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
	compileOnly "org.lwjgl:lwjgl"
	compileOnly "org.lwjgl:lwjgl-vulkan"
	compileOnly "org.lwjgl:lwjgl-glfw"
	compileOnly "org.lwjgl:lwjgl-vma"
	compileOnly "org.lwjgl:lwjgl-openxr"
	compileOnly "org.joml:joml:${jomlVersion}"

	testImplementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
	testImplementation "org.lwjgl:lwjgl"
	testImplementation "org.lwjgl:lwjgl-vma"
	testImplementation "org.lwjgl:lwjgl-vulkan"
	testImplementation "org.lwjgl:lwjgl-glfw"
	testImplementation "org.joml:joml:${jomlVersion}"

	testRuntimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
	testRuntimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
	testRuntimeOnly "org.lwjgl:lwjgl-vma::$lwjglNatives"
	if (lwjglNatives == "natives-macos") testRuntimeOnly "org.lwjgl:lwjgl-vulkan::$lwjglNatives"

	test {
		useJUnitPlatform()
	}
}

project(":samples") {
	apply plugin: "com.github.johnrengelman.shadow"
	project.ext.allNatives = [
			"natives-windows", "natives-windows-x86", "natives-windows-arm64",
			"natives-linux", "natives-linux-arm32", "natives-linux-arm64",
			"natives-freebsd", "natives-linux-ppc64le", "natives-linux-riscv64",
			"natives-macos", "natives-macos-arm64"
	]
    dependencies {
        implementation project(":")
        implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
        implementation "org.lwjgl:lwjgl"
        implementation "org.lwjgl:lwjgl-vma"
        implementation "org.lwjgl:lwjgl-vulkan"
        implementation "org.lwjgl:lwjgl-glfw"
        implementation "org.lwjgl:lwjgl-openxr"
        implementation "org.joml:joml:${jomlVersion}"

		for (String target : project.ext.allNatives) {
			runtimeOnly "org.lwjgl:lwjgl::$target"
			runtimeOnly "org.lwjgl:lwjgl-glfw::$target"
			runtimeOnly "org.lwjgl:lwjgl-vma::$target"
			if (target == "natives-macos" || target == "natives-macos-arm64") runtimeOnly "org.lwjgl:lwjgl-vulkan::$target"
			else runtimeOnly "org.lwjgl:lwjgl-openxr::$target"
		}
    }
    shadowJar {
        manifest {
            attributes ("Main-Class": "com.github.knokko.boiler.samples.MultipleWindows")
        }
    }
}
